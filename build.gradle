plugins {
    //support Idea IDE
    id 'idea'
    id "io.spring.dependency-management" version "1.0.10.RELEASE"
    // Build uber-jar
    id 'com.github.johnrengelman.shadow' version '6.1.0' apply false
}

idea {
    project {
        languageLevel = 15
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

allprojects {
    group "ru.otus"

    repositories {
        mavenLocal()
        mavenCentral()
    }
    apply plugin: "io.spring.dependency-management"
    dependencyManagement {
        dependencies {
            imports {
                mavenBom("org.springframework.boot:spring-boot-dependencies:2.5.2")
            }
            dependency("com.google.guava:guava:${Versions.guava}")
            dependency("org.openjdk.jmh:jmh-core:${Versions.jmh}")
            dependency("org.openjdk.jmh:jmh-generator-annprocess:${Versions.jmh}")
            dependency("org.openjdk.jol:jol-core:${Versions.jol}")
            dependency("org.ow2.asm:asm-commons:${Versions.asm}")
            dependency("org.glassfish:javax.json:${Versions.glassfishJson}")
            dependency("com.google.protobuf:protobuf-java-util:${Versions.protobuf}")
            dependency("org.testcontainers:junit-jupiter:${Versions.testcontainers}")
            dependency("org.testcontainers:postgresql:${Versions.testcontainers}")

            dependency("org.projectlombok:lombok:${Versions.lombok}")
            dependency("com.google.code.gson:gson:${Versions.gson}")
            dependency("com.datastax.oss:java-driver-core:${Versions.cassandra}")

            dependency ("org.mongodb:mongodb-driver-core:${Versions.mongodb}")
            dependency("org.mongodb:mongodb-driver-sync:${Versions.mongodb}")
            dependency("org.mongodb:bson:${Versions.mongodb}")

            dependency("org.mongodb:mongodb-driver-reactivestreams:${Versions.mongodbReactive}")

            dependency("org.neo4j.driver:neo4j-java-driver:${Versions.neo4j}")

            dependency("redis.clients:jedis:${Versions.jedis}")
        }
    }

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
        }
        resolutionStrategy {
            force("javax.servlet:servlet-api:2.4")
            force("commons-logging:commons-logging:1.1.1")
            force("commons-lang:commons-lang:2.5")
            force("org.codehaus.jackson:jackson-core-asl:1.8.8")
            force("org.codehaus.jackson:jackson-mapper-asl:1.8.3")
            force("org.codehaus.jettison:jettison:1.1")
            force("net.java.dev.jna:jna:5.8.0")
        }
    }
}